apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    compileSdkVersion parent.ext.compileSdkVersion
    buildToolsVersion parent.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.inftyloop.indulger"
        minSdkVersion parent.ext.minSdkVersion
        targetSdkVersion parent.ext.targetSdkVersion
        versionCode 1
        versionName "0.0.1(alpha)"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    Properties props = new Properties()
    def propFile = new File('app/signing.properties')
    if (propFile.canRead()){
        props.load(new FileInputStream(propFile))
        if (props !=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
            println 'Successfully loaded signing file'
            android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
            android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        } else {
            println 'signing.properties found but some entries are missing'
            android.buildTypes.release.signingConfig = null
            android.buildTypes.debug.signingConfig = null
        }
    } else {
        println 'signing.properties not found'
        android.buildTypes.release.signingConfig = null
        android.buildTypes.debug.signingConfig = null
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.0-beta01'
    implementation 'com.google.android.material:material:1.0.0-beta01'
    implementation 'androidx.recyclerview:recyclerview:1.0.0-beta01'
    implementation 'androidx.viewpager2:viewpager2:1.0.0-beta03'
    implementation project(':qmui')
    implementation project(':arch')
    implementation project(':switchButton')
    annotationProcessor project(':arch-compiler')
    implementation 'com.jakewharton:butterknife:10.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'cn.bingoogolapple:bga-refreshlayout:1.1.8@aar'
    implementation 'cn.jzvd:jiaozivideoplayer:7.0.5'
    implementation 'com.sunfusheng:GlideImageView:2.1.0'
    // database
    implementation 'org.litepal.android:java:3.0.0'
    // network
    implementation 'com.squareup.retrofit2:retrofit:2.6.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.6.1'
    implementation 'com.squareup.retrofit2:converter-scalars:2.6.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'io.reactivex:rxandroid:1.2.0'
    implementation 'com.github.easilycoder.HybridCache:hybridcache:0.0.1'
    implementation 'com.jakewharton:disklrucache:2.0.2'
    // event bus
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.49'
    // glide image
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
    // Wechat sdk
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:+'
}
